WHITESPACE = _{ " " | "\t" }

uuid = @{uuid_char{8} ~ "-" ~ uuid_char{4} ~ "-" ~ uuid_char{4} ~ "-" ~ uuid_char{4} ~ "-" ~ uuid_char{12}}
    uuid_char = _{(ASCII_ALPHA_LOWER | ASCII_DIGIT)}

alias = @{"$" ~ alias_char+}
    alias_char = @{ASCII_ALPHA | "-" | "_"}

session_command = {"session" ~ (session_op_show | session_op)}
    object = @{uuid | alias}
    session_op = _{object ~ (session_op_show | session_op_alias | session_op_open | session_op_remove)}
    session_op_show = {"show" | "sh"}
    session_op_alias = {("alias" | ">") ~ alias}
    session_op_open = {"open" | "op" | "."}
    session_op_remove = {"forget" | "x"}

ducky_command = {"ducky" ~ ducky_op}
    firmware_url = @{("https" | "http") ~ "://" ~ ((ASCII_ALPHANUMERIC)+ ~ ".")* ~ (ASCII_ALPHANUMERIC)+ ~ (":" ~ (ASCII_DIGIT)+)? ~ ("/" ~ (ASCII)*)?}
    ducky_op = _{ducky_op_flash}
    ducky_op_flash = {"uf2" ~ (firmware_url)?}

help_command = {("help" | "?") ~ (help_op)?}
    help_op = _{help_op_session}
    help_op_session = @{"session"}

command = _{ SOI ~ (session_command | ducky_command | help_command) ~ EOI }